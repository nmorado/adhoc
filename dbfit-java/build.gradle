allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply plugin: 'maven'
    group = 'com.neuri.dbfit'
    version = rootProject.dbfitVersion
    
    apply plugin: 'java'
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    apply plugin: 'checkstyle'

    checkstyle {
        configFile = new File(parent.projectDir, "config/checkstyle/checkstyle.xml")
    }
}

subprojects {
    jar.baseName = "dbfit-" + project.archivesBaseName
    repositories {
        mavenCentral()
        ivy {
            artifactPattern "https://s3.amazonaws.com/dbfit/[artifact]-[revision].[ext]"
            ivyPattern "https://aws.amazon.com/s3/ivy.xml"
        }
    }

    task libs(type: Copy) {
        from configurations.runtime
        into "$buildDir/libs"
    }

    dependencies {
        testRuntime 'com.dbdeploy:dbdeploy-ant:3.0M3'
        testCompile 'junit:junit:4.11'
        testCompile "org.hamcrest:hamcrest-all:1.3"
        testCompile "commons-io:commons-io:2.4"
    }

    ext.dbdeploy = { dbDriver, url, userid, password, delimiterType = 'normal' ->
        ant.taskdef(name: 'dbdeploy',
                classname: 'com.dbdeploy.AntTarget',
                classpath: configurations.testRuntime.asPath)
        ant.dbdeploy(driver: dbDriver,
                url: url,
                userid: userid,
                password: password,
                dir: "$projectDir/src/test/resources",
                delimiterType: delimiterType,
        )
    }

    test.inputs.dir file("$projectDir/../../FitNesseRoot/DbFit/AcceptanceTests/JavaTests")
}


task assembleAll(dependsOn: subprojects.assemble) { }
task libs(dependsOn: subprojects.libs) { }

project('core') {
    description = 'DBFit core api and fixtures'

    dependencies {
        compile "org.fitnesse:fitnesse:20130530"
        compile "dummy:fitlibrary:20081102@jar"
        compile "org.reflections:reflections:0.9.9-RC1"
        compile "commons-codec:commons-codec:1.7"
        testCompile "org.mockito:mockito-all:1.9.5"
    }
}

project('db2') {
    description = 'DBFit db2 engine'

    dependencies {
        compile project(':dbfit-java:core')
    }
}

project('derby') {
    description = 'DBFit derby engine'
    dependencies {
        compile project(':dbfit-java:core')
        testCompile group: 'org.apache.derby', name: 'derby', version: '10.9.1.0'
        testCompile group: 'org.apache.derby', name: 'derbynet', version: '10.9.1.0'
        testCompile group: 'org.apache.derby', name: 'derbyclient', version: '10.9.1.0'
    }
}

project('hsqldb') {
    description = 'DBFit HSQLDB engine'

    dependencies {
        compile project(':dbfit-java:core')
    }
}

project('oracle') {
    description = 'DBFit Oracle engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile "commons-io:commons-io:2.4"
        compile 'dummy:ojdbc6:11.2.0.3.0@jar'
        testCompile "org.mockito:mockito-all:1.9.5"
    }
}

project('mysql') {
    description = 'DBFit MySQL engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.6'
    }

    test.doFirst {
        dbdeploy('com.mysql.jdbc.Driver', 'jdbc:mysql://127.0.0.1/dbfit', 'dbfit_user', 'password')
    }
}

project('postgres') {
    description = 'DBFit PostGreSQL engine'

    dependencies {
        compile project(':dbfit-java:core')
        compile group: 'postgresql', name: 'postgresql', version: '9.1-901.jdbc4'
    }

    test.doFirst {
        dbdeploy('org.postgresql.Driver', 'jdbc:postgresql:dbfit', 'dbfit', 'dbfit', 'row')
    }
}

project('sqlserver') {
    description = 'DBFit sqlserver engine'

    dependencies {
        compile group: 'net.sourceforge.jtds', name: 'jtds', version: '1.2.2'
        compile project(':dbfit-java:core')
    }
}

project('teradata') {
    description = 'DBFit Teradata engine'

    dependencies {
        compile project(':dbfit-java:core')
    }
}

